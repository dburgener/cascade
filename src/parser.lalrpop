// Copyright (c) Microsoft Corporation.
// SPDX-License-Identifier: MIT
use crate::ast::{CascadeString, Policy, Declaration, DeclarationModifier, Expression, Statement, TypeDecl, FuncDecl, Argument, Annotation, LetBinding, Virtualable, FuncCall, DeclaredArgument, Module, Machine, MachineBody, Port, IpAddr as AstIpAddr, IfBlock};
use lalrpop_util::ErrorRecovery;
use lalrpop_util::ParseError;
use crate::error::ParseErrorMsg;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, ParseErrorMsg>>);

extern {
	type Error = ParseErrorMsg;
}

// http://lalrpop.github.io/lalrpop/tutorial/006_macros.html
Comma<T>: Vec<T> = {
	<mut v:(<T> ",")*> <e:T?> => match e {
		None => v,
		Some(e) => {
			v.push(e);
			v
		}
	}
};

pub Policy: Box<Policy> = {
	Expr+ => Box::new(Policy::new(<>)),
}

Annotated<T>: T = {
	<a:Ann> <mut t:Annotated<T>> => {
		t.add_annotation(a);
		t
	},
	T
}

pub Expr: Expression = {
	Annotated<BaseExpr>,
	// On error, report and fast forward to the next expression
	! => { errors.push(<>); Expression::Error },
}

BaseExpr: Expression = {
	<m: DeclModifier?> <mut d: Decl> =>? {
		match m {
			Some(DeclarationModifier::Virtual(range)) => {
				match d.set_virtual(range) {
					Ok(()) => (),
					Err(e) => return Err(ParseError::User {
						error: e})
				}
			}
			Some(DeclarationModifier::Trait(range)) => {
				match d.set_trait(range) {
					Ok(()) => (),
					Err(e) => return Err(ParseError::User {
						error: e})
				}
			}
			None => ()
		}
		Ok(Expression::Decl(d))
	},
	<Stmt> => Expression::Stmt(<>),
}

DeclModifier: DeclarationModifier = {
	<start: @L> <v: "virtual"> <end: @R> => DeclarationModifier::Virtual(start..end),
	<start: @L> <t: "trait"> <end: @R> => DeclarationModifier::Trait(start..end),
}

Decl: Declaration = {
	TypeDecl => Declaration::Type(<>),
	FuncDecl => Declaration::Func(<>),
	ModuleDecl => Declaration::Mod(<>),
	MachineDecl => Declaration::Machine(<>),
}

TypeDecl: Box<TypeDecl> = {
	<keyword: BuiltInOrExtend> <n:NameDecl> <i:InheritList?> "{" <mut v:Expr*> "}" => {
		let mut inherits = i.unwrap_or_else(|| Vec::new());
		let mut is_extend = false;
		match keyword {
			Some(dr) => inherits.push(dr),
			None => is_extend = true,
		}
		v.iter_mut().for_each(|e| e.set_class_name_if_decl(n.clone()));
		let mut td = TypeDecl::new(n, inherits, v);
		if is_extend {
			td.set_extend();
		}
		Box::new(td)
	},
}

BuiltInOrExtend: Option<CascadeString> = {
	BuiltInType => Some(<>),
	"extend" => None,
}

InheritList: Vec<CascadeString> = {
	"inherits" <Comma<Symbol>>,
}

BuiltInType: CascadeString = {
	<start: @L> <s: "domain"> <end: @R> => CascadeString::new(s.to_string(), start..end),
	<start: @L> <s: "resource"> <end: @R> => CascadeString::new(s.to_string(), start..end),
}

FuncDecl: Box<FuncDecl> = {
	"fn" <n: NameDecl> "(" <a: Comma<FuncDeclArg>> ")" "{" <b: Stmt*> "}" => Box::new(FuncDecl::new(n, a, b)),
}

FuncDeclArg: DeclaredArgument = {
	<t: Symbol> <n: NameDecl> <v: DefaultArg?> => DeclaredArgument { param_type: t, is_list_param: false, name: n, default: v },
	"[" <t: Symbol> "]" <n: NameDecl> <v: DefaultArg?> => DeclaredArgument { param_type: t, is_list_param: false, name: n, default: v },
}

#[inline]
DefaultArg: Argument = {
	"=" <Arg> => <>
}

Stmt: Statement = {
	<StmtBody> ";",
	<IfBlock> => Statement::IfBlock(Box::new(<>)),
	<start: @L> "drop" <end: @R> <mut s: StmtBody> ";" =>? {
		s.set_drop(start..end)?;
		Ok(s)
		}
}

StmtBody: Statement = {
	<c:(<Casted<TypeName>> ".")?> <n:NameDecl> "(" <a:Comma<CastArg>> ")" => Statement::Call(Box::new(FuncCall::new_with_casts(c, n, a))),
	<l:LetBind> => Statement::LetBinding(Box::new(<>)),
}

LetBind: LetBinding = {
	"let" <n:Symbol> "=" <a:Arg> => LetBinding::new(n, a),
}

ModuleDecl: Module = {
	"module" <n: NameDecl> "{" <x: ModuleBody*> "}" => Module::new(n).set_fields(x),
}

ModuleBody: (CascadeString, CascadeString) = {
	<s: BuiltInType> <n: NameDecl> ";" => (s, n),
	<start: @L> <s: "module"> <end: @R> <n: NameDecl> ";" => (CascadeString::new(s.to_string(), start..end), n),
}

MachineDecl: Machine = {
	"machine" <n: NameDecl> "{" <x: SysBody*> "}" => Machine::new(n).set_fields(x),
}

SysBody: MachineBody = {
	"module" <n: NameDecl> ";" => MachineBody::Mod(n),
	<l:LetBind> ";" => MachineBody::Config(l),
}

Ann: Annotation = {
	"@" <s:NameDecl> "(" <a:Comma<Arg>> ")" => Annotation::new(s).set_arguments(a),
	"@" <s:NameDecl> => Annotation::new(s),
}

TypeName: CascadeString = {
	Symbol,
	<start: @L> <l: TypeName> "." <r: Symbol> <end: @R> => CascadeString::new([l.as_ref(), ".", r.as_ref()].concat(), start..end),
	<start: @L> "*" <end: @R> => CascadeString::new("*".to_string(), start..end)
}

pub NameDecl: CascadeString = {
	// Naming rules:
	// * must start with a letter
	// * must not end with an underscore
	// * must not contain consecutive underscores
	// * can contain letters, digits and underscores
	<start: @L> <s: r"[a-zA-Z](_?([0-9a-zA-Z]+_)*[0-9a-zA-Z]+)?"> <end: @R>  => CascadeString::new(s.to_string(), start..end),
}

Symbol: CascadeString = {
	NameDecl,
	BuiltInType
}

List: Vec<CascadeString> = {
	"[" <TypeName+> "]"
}

// TODO: Define boolean struct
BooleanExpr: () = {
	BoolTerm,
	BooleanExpr "&&" BoolTerm,
	BooleanExpr "||" BoolTerm,
}

BoolTerm: () = {
	Symbol,
	"(" BooleanExpr ")",
	"!" BoolTerm,
}

IfBlock: IfBlock = {
	<start: @L> "if" <end: @R> "(" BooleanExpr ")" "{" <then: Stmt+> "}" <e: ElseBlock?> => {
		IfBlock {
			keyword_range: start..end,
			if_statements: then,
			else_statements: match e {
				Some(e) => e,
				None => Vec::new()
			}
		}}
}

#[inline]
ElseBlock: Vec<Statement> = {
	"else" "{" <Stmt+> "}" => <>
}

#[inline]
Casted<T>: (T, Option<CascadeString>) = {
	<t: T> <c:("<" <TypeName> ">")?> => (t, c),
}

CastArg: (Argument, Option<CascadeString>) = {
	Casted<Arg>,
}

Arg: Argument = {
	TypeName => Argument::Var(<>),
	<s:TypeName> "=" <a: Arg> => Argument::Named(s, Box::new(a)),
	List => Argument::List(<>),
	Quoted_String => Argument::Quote(<>),
	Port => Argument::Port(<>),
	IPAddr => Argument::IpAddr(<>),
}

Quoted_String: CascadeString = {
	<start: @L> <s: r#""[^"]*""#> <end: @R> => CascadeString::new(s.to_string(), start..end),
}

Port: Port = {
	<start: @L> <p: PortRegex> <end: @R> => Port::new(p.parse().unwrap(), Some(start..end)) // TODO: report parse error instead of panicing
}

IPAddr: AstIpAddr = {
	IPv4 => AstIpAddr::new(<>.as_ref().parse().unwrap(), <>.get_range()), // TODO
	IPv6 => AstIpAddr::new(<>.as_ref().parse().unwrap(), <>.get_range()), // TODO,
}

IPv4: CascadeString = {
	<start: @L> <ip: IPv4Regex> <end: @R> => CascadeString::new(ip.to_string(), start..end),
	<start: @L> "localhost" <end: @R> => CascadeString::new("localhost".to_string(), start..end),
}

IPv6: CascadeString = {
	<start: @L> <ip: IPv6Regex> <end: @R> => CascadeString::new(ip.to_string(), start..end),
}

// lexing precedence
match {
	r"\s*" => { },
	r"//[^\n\r]*[\n\r]*" => { },
	r"(\d{1,3}\.){4}" => IPv4Regex,
	"::1" => IPv6Regex, // TODO
	r"\d+" => PortRegex,
} else {
	_
}
